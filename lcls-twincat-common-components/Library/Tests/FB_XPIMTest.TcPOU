<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_XPIMTest" Id="{fe1291e2-e066-4812-8d3e-bf776c10c3c5}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_XPIMTest EXTENDS FB_TestSuite
VAR
    fbXPIM: FB_XPIM;
    stYStage: ST_MotionStage;
    stZoomStage: ST_MotionStage;
    stFocusStage: ST_MotionStage;

    stEl6In: EL6inData22b;
    stEl6Out: EL6OutData22b;

    stDefault: ST_PositionState := (
        fVelocity:=10,
        bMoveOk:=TRUE,
        bValid:=TRUE
    );
    fbSetup: FB_StateSetupHelper;

    fbFFHWO: FB_HardwareFFOutput;
    fbArbiter: FB_Arbiter(1);
    fbSubSysIO: FB_DummyArbIO;

    bInit: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Fake PMPS handling
fbSubSysIO(
    LA:=fbArbiter,
    FFO:=fbFFHWO,
);

// Fake limit handling
stYStage.bLimitBackwardEnable := TRUE;
stYStage.bLimitForwardEnable := TRUE;

// Standard state setup
fbSetup(stPositionState:=stDefault, bSetDefault:=TRUE);
fbSetup(stPositionState:=fbXPIM.stOut, sName:='OUT', fPosition:=10, sPmpsState:='T0');
fbSetup(stPositionState:=fbXPIM.stReticle, sName:='T1', fPosition:=20, sPmpsState:='T1');
fbSetup(stPositionState:=fbXPIM.stYag, sName:='T2', fPosition:=30, sPmpsState:='T2');
fbSetup(stPositionState:=fbXPIM.stDiamond, sName:='T3', fPosition:=40, sPmpsState:='T3');

// Standard FB call
fbXPIM(
    stYStage:=stYStage,
    stZoomStage:=stZoomStage,
    stFocusStage:=stFocusStage,
    fbFFHWO:=fbFFHWO,
    fbArbiter:=fbArbiter,
    stEl6In:=stEl6In,
    stEl6Out:=stEl6Out,
    bEnableMotion:=TRUE,
    bEnableBeamParams:=TRUE,
    bEnablePositionLimits:=TRUE,
    sDeviceName:='DEVICE',
    sTransitionKey:='T9',
    bReadDBNow:=NOT bInit,
);

TestStateMove();

bInit := TRUE;
]]></ST>
    </Implementation>
    <Method Name="TestStateMove" Id="{e6443622-06be-4997-85db-426ede8e4383}">
      <Declaration><![CDATA[METHOD TestStateMove
VAR_INST
    tonTimer: TON;
    nIterState: UINT := 0;
END_VAR
VAR CONSTANT
    nLastState: UINT := E_XPIM_States.RETICLE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Sanity check: can we at least move to every named state?
TEST('TestXPIMStateMove');

// Prepare a timeout
tonTimer(IN:=TRUE, PT:=T#10s);

// Start in Unknown, then go through the state positions one by one
IF fbXPIM.eEnumGet = nIterState THEN
    nIterState := nIterState + 1;
    fbXPIM.eEnumSet := nIterState;
END_IF

IF tonTimer.Q OR nIterState > nLastState THEN
    AssertFalse(tonTimer.Q, 'Timeout in XPIM move test');
    TEST_FINISHED();
END_IF
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>