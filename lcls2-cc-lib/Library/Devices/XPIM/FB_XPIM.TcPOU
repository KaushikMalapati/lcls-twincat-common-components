<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_XPIM" Id="{aa9f645d-c463-4c9e-8962-8aa3b3b2b9c3}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_XPIM
VAR_IN_OUT
    stYStage: DUT_MotionStage;
    stZoomStage: DUT_MotionStage;
    stFocusStage: DUT_MotionStage;
    stEl6In: EL6inData22b;
    stEl6Out: EL6OutData22b;
END_VAR
VAR_INPUT
    {attribute 'pytmc' := '
        pv: CLZ:LOCK
        io: io
    '}
    bZoomLock: BOOL;

    {attribute 'pytmc' := '
        pv: CLF:LOCK
        io: io
    '}
    bFocusLock: BOOL;

    bZoomEndFwd AT %I*: BOOL;
    bZoomEndBwd AT %I*: BOOL;
    bFocusEndFwd AT %I*: BOOL;
    bFocusEndBwd AT %I*: BOOL;

    fYag: LREAL;
    fDiamond: LREAL;
    fReticle: LREAL;
    fOut: LREAL;
END_VAR
VAR
    fbYStage: FB_MotionStage;
    fbZoom: FB_MotionStage;
    fbFocus: FB_MotionStage;

    {attribute 'pytmc' := '
        pv: MMS:STATE
        io: io
    '}
    fbStates: FB_XPIM_States;

    {attribute 'pytmc' := '
        pv: MFW
    '}
    fbFilterWheel: FB_XPIM_FilterWheel;

    {attribute 'pytmc' := '
        pv: CAM
    '}
    fbOpal: FB_XPIM_Opal;

    {attribute 'pytmc' := '
        pv: CIL
    '}
    fbLED: FB_XPIM_LED;

    {attribute 'pytmc' := '
        pv: SFW
    '}
    fbFlowSwitch: FB_XTES_Flowswitch;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// All stages have no STO, but there is a user lock on the lens
stYStage.bHardwareEnable := TRUE;
stZoomStage.bHardwareEnable := NOT bZoomLock;
stFocusStage.bHardwareEnable := NOT bFocusLock;

// No PMPS yet
stYStage.bPowerSelf := TRUE;
stZoomStage.bPowerSelf := TRUE;
stFocusStage.bPowerSelf := TRUE;

// No limit switch at the bottom
stYStage.bLimitBackwardEnable := TRUE;

// Lens limits are normally open
stZoomStage.bLimitForwardEnable := NOT bZoomEndFwd;
stZoomStage.bLimitBackwardEnable := NOT bZoomEndBwd;
stFocusStage.bLimitForwardEnable := NOT bFocusEndFwd;
stFocusStage.bLimitBackwardEnable := NOT bFocusEndBwd;

// Home Lens to LLS
stZoomStage.nHomingMode := ENUM_EpicsHomeCmd.LOW_LIMIT;
stFocusStage.nHomingMode := ENUM_EpicsHomeCmd.LOW_LIMIT;

fbYStage(stMotionStage:=stYStage);
fbZoom(stMotionStage:=stZoomStage);
fbFocus(stMotionStage:=stFocusStage);

fbStates(
    stMotionStage:=stYStage,
    fYag:=fYag,
    fDiamond:=fDiamond,
    fReticle:=fReticle,
    fOut:=fOut);

fbFilterWheel(
    bExecute:=TRUE,
    stIn_El6:=stEl6In,
    stOut_El6:=stEl6Out);

fbOpal();
fbLED(enumXPIM:=fbStates.enumGet);
fbFlowSwitch();]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>